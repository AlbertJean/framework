static const char * s_hqCommonPs = R"SHADER(

#if _SHADER_DEBUGGING_
	uniform float disableOptimizations;
	uniform float disableAA;
	uniform float _debugHq;
	
	#define enableOptimizations (disableOptimizations == 0.0)
	#define enableAA (disableAA == 0.0)
	#define debugHq (_debugHq != 0.0)
#else
	#define enableOptimizations true
	#define enableAA true
	#define debugHq false
#endif

shader_in vec2 v_texcoord;

uniform sampler2D source;
uniform float sourceEnabled;

float computeDistance(vec2 p);
float distanceFade(float distance);

bool sampleDistanceFunctionHasBeenCalled = false;

float sampleDistanceFunction(vec2 p)
{
	float v = 0.0;

	sampleDistanceFunctionHasBeenCalled = true;
	
	if (enableAA)
	{
		for (int ox = -2; ox <= +2; ++ox)
		{
			for (int oy = -2; oy <= +2; ++oy)
			{
				vec2 offset = vec2(ox, oy) * 0.25;
				
				v += distanceFade(computeDistance(p + offset));
			}
		}
		
		v /= 25.0;
	}
	else
	{
		v = distanceFade(computeDistance(p));
	}
	
	return v;
}

vec4 hqColor(float v, vec4 color)
{
	color.a *= v;
	
	if (debugHq)
	{
		if (sampleDistanceFunctionHasBeenCalled == false)
		{
			color.rgb += vec3(0.0, 0.2, 0.5);
			color.a += 0.5;
		}
		else
		{
			color.rgb += vec3(0.5, 0.0, 0.0);
			color.a += 0.5;
		}

		color = clamp(color, vec4(0.0), vec4(1.0));
	}
	
	if (sourceEnabled != 0.0)
	{
		color *= texture(source, v_texcoord);
	}

	return color;
}

)SHADER";
